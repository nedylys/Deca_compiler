parser grammar DecaParser;

options {
    // Default language but name it anyway
    //
    language  = Java;

    // Use a superclass to implement all helper
    // methods, instance variables and overrides
    // of ANTLR default methods, such as error
    // handling.
    //
    superClass = AbstractDecaParser ;

    // Use the vocabulary generated by the accompanying
    // lexer. Maven knows how to work out the relationship
    // between the lexer and parser and will build the
    // lexer before the parser. It will also rebuild the
    // parser if the lexer changes.
    //
    tokenVocab = DecaLexer;

}

// which packages should be imported?
@header {
    import fr.ensimag.deca.tree.*;
    import java.io.PrintStream;
    import java.util.*;
    import fr.ensimag.deca.tools.*;
    import org.apache.commons.lang.Validate;
}

@members {
    @Override
    protected AbstractProgram parseProgram() {
        return prog().tree;
    }
}

prog returns[AbstractProgram tree]
    : list_classes main EOF {
            assert($list_classes.tree != null);
            assert($main.tree != null);
            $tree = new Program($list_classes.tree, $main.tree);
            setLocation($tree, $list_classes.start);
        }
    ;

main returns[AbstractMain tree]
    : /* epsilon */ {
            $tree = new EmptyMain();
        }
    | block {
            assert($block.decls != null);
            assert($block.insts != null);
            $tree = new Main($block.decls, $block.insts);
            setLocation($tree, $block.start);
        }
    ;

block returns[ListDeclVar decls, ListInst insts]
    : OBRACE list_decl list_inst CBRACE {
            assert($list_decl.tree != null);
            assert($list_inst.tree != null);
            $decls = $list_decl.tree;
            $insts = $list_inst.tree;

            setLocation($decls, $OBRACE) ;
            setLocation($insts, $OBRACE) ;
        }
    ;

list_decl returns[ListDeclVar tree]
@init   {
            $tree = new ListDeclVar();
        }
    : decl_var_set[$tree]*
    {setLocation($tree, $ctx.start);}
    ;

decl_var_set[ListDeclVar l]
    : type list_decl_var[$l,$type.tree] SEMI
    ;

list_decl_var[ListDeclVar l, AbstractIdentifier t]
    : dv1=decl_var[$t] {
        $l.add($dv1.tree);
        } (COMMA dv2=decl_var[$t] {
            $l.add($dv2.tree);
        }
      )*
    ;

decl_var[AbstractIdentifier t] returns[AbstractDeclVar tree] // t ici représente le type
//  @init   {
//     AbstractInitialization init = new NoInitialization();
//         } 
//         //ident = identifier
//     : i=ident
//     // Les arguments de DeclVar sont type (AbstractIdentifier), varname (AbstractIdentifier) et optionnellement initialization (AbstractInitialization)
//     // Cas d'assignement
//       (EQUALS e=expr {
//         init = new Initialization($e.tree);
//         }
//       )? 
//       {
//         $tree = new DeclVar($t, $i.tree, init);
//         setLocation($tree,$i.start);
//       }
//     ;
//MODIF LE 1 JANV
    : i=ident
      (EQUALS e=expr {
            Initialization init = new Initialization($e.tree);
            setLocation(init, $e.start);
            $tree = new DeclVar($t, $i.tree, init);
        }
      )?
      {
        if ($tree == null) {
            NoInitialization init = new NoInitialization();
            setLocation(init, $i.start);
            $tree = new DeclVar($t, $i.tree, init);
        }
        setLocation($tree, $i.start);
      }
    ;

list_inst returns[ListInst tree]
/* Il nous faut un assert */
@init {
        $tree = new ListInst();
}
    : (inst {
            assert($inst.tree != null); 
            $tree.add($inst.tree);
        }
      )*
    ;

inst returns[AbstractInst tree]
    : e1=expr SEMI {
            assert($e1.tree != null);
            $tree = $e1.tree;
            setLocation($tree,$e1.start);
        }
    /*; toute seule est une instruction vide (no Operation plutot) */
    | SEMI {
        $tree = new NoOperation();
        setLocation($tree,$SEMI);
        }
     | PRINT OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Print(false,$list_expr.tree); 
            setLocation($tree,$PRINT); /* PAS SUR DU $PRINT */
        } 
    | PRINTLN OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Println(false,$list_expr.tree);
            setLocation($tree,$PRINTLN);
        }
    | PRINTX OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Print(true,$list_expr.tree);
            setLocation($tree,$PRINTX);    
        } 
    | PRINTLNX OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Println(true,$list_expr.tree);
            setLocation($tree,$PRINTLNX);
        } 
    | if_then_else {
        assert($if_then_else.tree != null);
        $tree = $if_then_else.tree;
        setLocation($tree,$if_then_else.start);
        } 
    | WHILE OPARENT condition=expr CPARENT OBRACE body=list_inst CBRACE {
            assert($condition.tree != null);
            assert($body.tree != null);
            $tree = new While($condition.tree,$body.tree);
            setLocation($tree,$WHILE);
        } 
    | RETURN expr SEMI {
            assert($expr.tree != null);
            $tree = new Return($expr.tree);
            setLocation($tree,$expr.start);
        } 
    ;

if_then_else returns[IfThenElse tree]
@init {
    // List of conditions
    ArrayList<AbstractExpr> listCond = new ArrayList<>();

    // List of instructions

    ArrayList<ListInst> listInst = new ArrayList<>();
    ListInst elseBlock = null;
}
    /*The very first if */
    : if1=IF OPARENT condition=expr CPARENT OBRACE li_if=list_inst CBRACE {
          assert($condition.tree != null); // Not necessary as if expr succeeds it is impossible for it to be null, but let's keep it for extra security
          listCond.add($condition.tree);
          listInst.add($li_if.tree);
        }
    /*Else if 's */
      (ELSE elsif=IF OPARENT elsif_cond=expr CPARENT OBRACE elsif_li=list_inst CBRACE {
        assert($elsif_cond.tree != null); // Again : Not necessary as if expr succeeds it is impossible for it to be null, but let's keep it for extra security
        listCond.add($elsif_cond.tree);
        listInst.add($elsif_li.tree);        }
      )*

      (ELSE OBRACE li_else=list_inst CBRACE {

        elseBlock = $li_else.tree;
        setLocation(elseBlock, $if1);
        }
      )?

      {
        /*The idea is that we have to start from the else and go up as we get more information : in fact else is the only block 
        that doesn't require external information , its else_if on the other hand needs the else so it has to be ready for it to work.
        As for the else if's they will be treated as else {if() ... else} .
        In Other words we will create the tree going from right to left.*/

        IfThenElse actualNode = null; //an else has no more IfThenElse

        int dephtTree = listCond.size();

        // Creating the first IfThenELse node (which is the last one in the program (right of the tree))

        if (elseBlock == null) {
            // In case There is no else
            elseBlock = new ListInst();
            setLocation(elseBlock, $if1);
        }

        actualNode = new IfThenElse(listCond.get(dephtTree - 1),listInst.get(dephtTree - 1),elseBlock);
        setLocation(actualNode, $if1);

        for (int i = dephtTree - 2; i >= 0; i--) {
            // Here we start creating the else if nodes if they do exist
            ListInst Block = new ListInst();
            setLocation(Block, $if1);
            // We add the ifThenElse of this elseif = else{if (..) else}
            Block.add(actualNode);

            actualNode = new IfThenElse(listCond.get(i), // If Condition
                                        listInst.get(i), // Then
                                        Block);          // Else
            setLocation(actualNode, $if1);
        }

        // At the end of the loop actualNode points towards the very first if which gives the rest of the tree
        $tree = actualNode;
        setLocation($tree,$if1);

      }
    ;

list_expr returns[ListExpr tree]
@init{
        $tree = new ListExpr();
        }
    :(e1=expr {
            assert($e1.tree != null); // encore pas nécessaire
            $tree.add($e1.tree);
        }
    (COMMA e2=expr {
            assert($e2.tree != null); // not necessary
            $tree.add($e2.tree);
            
        })*
       )?
       
    ;


expr returns[AbstractExpr tree]
    : assign_expr {
            assert($assign_expr.tree != null);
            $tree = $assign_expr.tree;
        }
    ;

assign_expr returns[AbstractExpr tree]
    : e=or_expr (
        /* condition: expression e must be a "LVALUE" */ {
            if (! ($e.tree instanceof AbstractLValue)) {
                throw new InvalidLValue(this, $ctx);
            }
        }
        EQUALS e2=assign_expr {
            assert($e.tree != null);
            assert($e2.tree != null);
            $tree = new Assign((AbstractLValue) $e.tree,$e2.tree);
            setLocation($tree,$EQUALS);
        }
      | /* epsilon */ {
            assert($e.tree != null);
            $tree = $e.tree;
        }
      )
    ;

or_expr returns[AbstractExpr tree]
    : e=and_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree,$e.start);
        }
    /*Si on a une regle or il faudra retourner un arbre qui correspond à e1 or e2 */
    | e1=or_expr OR e2=and_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Or($e1.tree,$e2.tree);
            setLocation($tree,$OR);
       }
    ;

and_expr returns[AbstractExpr tree]
    : e=eq_neq_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree,$e.start);
        }    
    |  e1=and_expr AND e2=eq_neq_expr {
            assert($e1.tree != null);                         
            assert($e2.tree != null);
            $tree = new And($e1.tree,$e2.tree);
            setLocation($tree,$AND);
        }
    ;

eq_neq_expr returns[AbstractExpr tree]
    : e=inequality_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree,$e.start);
        }
    /* idem que AND */
    | e1=eq_neq_expr EQEQ e2=inequality_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Equals($e1.tree,$e2.tree);
            setLocation($tree,$EQEQ);
        }
    | e1=eq_neq_expr NEQ e2=inequality_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new NotEquals($e1.tree,$e2.tree);
            setLocation($tree,$NEQ);
        }
    | e=inequality_expr INSTANCEOF t=type {
            assert($e.tree != null);
            assert($t.tree != null);
            $tree = new InstanceOf($e.tree,$t.tree);
            setLocation($tree,$INSTANCEOF);
        }  
    ;

inequality_expr returns[AbstractExpr tree]
    : e=sum_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree,$e.start);
        }
    /* idem que AND*/
    | e1=inequality_expr LEQ e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new LowerOrEqual($e1.tree,$e2.tree);
            setLocation($tree,$LEQ);
        }
    | e1=inequality_expr GEQ e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new GreaterOrEqual($e1.tree,$e2.tree);
            setLocation($tree,$GEQ);
        }
    | e1=inequality_expr GT e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Greater($e1.tree,$e2.tree);
            setLocation($tree,$GT);
        }
    | e1=inequality_expr LT e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Lower($e1.tree,$e2.tree);
            setLocation($tree,$LT);
        }
   /*  | e1=inequality_expr INSTANCEOF type {
            assert($e1.tree != null);
            assert($type.tree != null);
        } */
    ;


sum_expr returns[AbstractExpr tree]
    : e=mult_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree,$e.start); 
        }

    /* idem que AND */
    | e1=sum_expr PLUS e2=mult_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Plus($e1.tree, $e2.tree);
            setLocation($tree,$PLUS);
        }
    | e1=sum_expr MINUS e2=mult_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Minus($e1.tree, $e2.tree);
            setLocation($tree,$MINUS);
        }
    ;

mult_expr returns[AbstractExpr tree]
    : e=unary_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree,$e.start);
        }
    /* idem que AND */
    | e1=mult_expr TIMES e2=unary_expr {
            assert($e1.tree != null);                                         
            assert($e2.tree != null);
            $tree = new Multiply($e1.tree,$e2.tree);
            setLocation($tree,$TIMES);
        }
    | e1=mult_expr SLASH e2=unary_expr {
            assert($e1.tree != null);                                         
            assert($e2.tree != null);
            $tree = new Divide($e1.tree,$e2.tree);
            setLocation($tree,$SLASH);
        }
    | e1=mult_expr PERCENT e2=unary_expr {
            assert($e1.tree != null);                                                                          
            assert($e2.tree != null);
            $tree = new Modulo($e1.tree,$e2.tree);
            setLocation($tree,$PERCENT);
        }
    ;

unary_expr returns[AbstractExpr tree]
    : op=MINUS e=unary_expr {
            assert($e.tree != null);
            $tree = new UnaryMinus($e.tree);
            setLocation($tree,$op);
        }
    | op=EXCLAM e=unary_expr {
            assert($e.tree != null);
            $tree = new Not($e.tree);
            setLocation($tree,$op);
        } 
    | select_expr {
            assert($select_expr.tree != null);
            $tree = $select_expr.tree;
            setLocation($tree,$select_expr.start);
        }
    ;

select_expr returns[AbstractExpr tree]
@init {
    /* ListExpr tmpListExpr = new ListExpr(); */
}   
    /*for access to methods without using '.' aka this.f() */
    : e=primary_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree,$e.start);
        }
     | e1=select_expr DOT i=ident // I will follow the grammar to the letter
        (   // Method call
            o=OPARENT args=list_expr CPARENT {
            assert($args.tree != null); // again not necessary
            assert($e1.tree != null);
            assert($i.tree != null);

            $tree = new MethodCall($e1.tree,$i.tree,$args.tree); // e1.tree = object; i.tree = method's name and args.tree si for arguments
            setLocation($args.tree, $o); // ajout le 3 janv
            setLocation($tree,$DOT); // DOT is the most telling token in a selection
        } 
        |/* epsilon : Access to a field*/
        {
            assert($e1.tree != null); // again not necessary
            assert($i.tree != null);
            
            $tree = new Selection($i.tree,$e1.tree); // e1.tree = object; i.tree = field's name
            setLocation($tree,$DOT);
        } 
        )
    ;

primary_expr returns[AbstractExpr tree]
    : ident {
            assert($ident.tree != null);
            $tree = $ident.tree;
            setLocation($tree,$ident.start);
        }
    | m=ident OPARENT args=list_expr CPARENT {
            assert($args.tree != null);
            assert($m.tree != null);
            This t = new This(true) ;
            setLocation(t, $m.start) ;
            $tree = new MethodCall(t, $m.tree,$args.tree);;      
            setLocation($tree,$OPARENT);
        } 
    | OPARENT expr CPARENT {
            assert($expr.tree != null);
            $tree = $expr.tree;
            setLocation($tree,$OPARENT);
        }
     | READINT OPARENT CPARENT {
        $tree = new ReadInt();
        setLocation($tree,$READINT); 
         } 
     | READFLOAT OPARENT CPARENT { 
        $tree = new ReadFloat();
        setLocation($tree,$READFLOAT); 
         } 
     | NEW ident OPARENT CPARENT {
            assert($ident.tree != null);
            $tree = new New($ident.tree);
            setLocation($tree,$NEW);
        } 
    | OPARENT t=type CPARENT OPARENT e=expr CPARENT {
            assert($type.tree != null);
            assert($expr.tree != null);
            $tree = new Cast($t.tree,$e.tree);
            setLocation($tree,$OPARENT);
        } 
    | literal {
            assert($literal.tree != null);
            $tree = $literal.tree;
            setLocation($tree,$literal.start);
        }
    ;

type returns[AbstractIdentifier tree]
    : ident {
        assert($ident.tree != null);
        $tree = $ident.tree;
        setLocation($tree,$ident.start);
    }
    ;

literal returns[AbstractExpr tree]
    : i=INT {
            $tree = new IntLiteral(Integer.parseInt($i.text));
            setLocation($tree,$i);
        }
    | fd=FLOAT {
            float f = Float.parseFloat($fd.text);
            String fStr = $fd.text;
            int taille = fStr.length();
            if (f == 0 ){
                for (int i = 0; i < taille;i++){
                    char c = fStr.charAt(i);
                    if (c >= '1' && c <= '9'){
                        Validate.isTrue(false,"Float underflow");
                     }
                }
            }
            $tree = new FloatLiteral(f);
            setLocation($tree, $fd);
        }
        // un STRING.text contient les guillemets, il faut les enlever en faisant un substring
    | sr=STRING {
        $tree = new StringLiteral($sr.text.substring(1, $sr.text.length()-1));
        setLocation($tree,$sr);
        }
    | tr=TRUE {
        $tree = new BooleanLiteral(true);
        setLocation($tree,$tr);
        }
    | fa=FALSE {
        $tree = new BooleanLiteral(false);
        setLocation($tree,$fa);
        }
     | THIS {
        $tree = new This(false);
        setLocation($tree,$THIS);
        }
    | NULL {
        $tree = new Null();
        setLocation($tree, $NULL);
        } 
    ;

ident returns[AbstractIdentifier tree]
    : IDENT {
        $tree = new Identifier(getDecacCompiler().symbolTable.create($IDENT.text));
        setLocation($tree, $IDENT);
        }
    ;

/****     Class related rules     ****/

list_classes returns[ListDeclClass tree]
@init { 

    $tree = new ListDeclClass();  
}
    /*Adding classes as we meet them */
    : (c=class_decl {
        $tree.add($c.tree);
      })*
    ;


class_decl returns[AbstractDeclClass tree]
@init{ 
    ListDeclMethod listMethods = new ListDeclMethod();
    ListDeclField listFields = new ListDeclField(); 
 }
    : CLASS name=ident superclass=class_extension /* CLASS A extends superClass  */
     OBRACE /*{ */
    /*We need to initialiez fields and methods for class_body to fill */
    /*class Body = fields + methods */
    class_body[listFields, listMethods]
     CBRACE /*}*/
     /*Now that everything is set we call DeclClass (step B)*/{
        $tree = new DeclClass(
            $name.tree,
            $superclass.tree,       
            listFields,
            listMethods     
        );
        setLocation($tree, $name.start);
      }
    ;


class_extension returns[AbstractIdentifier tree]
// Il faut trouver une solution pour le setLocation de Object
    : EXTENDS ident {
        $tree = $ident.tree;
        setLocation($tree,$EXTENDS);
        }
    | /* epsilon */ {
        $tree = new Identifier(getDecacCompiler().createSymbol("Object")); 
        setLocation($tree,$ctx.start);
        }
    ;

class_body[ListDeclField listFields, ListDeclMethod listMethods]
    : (m=decl_method {
        listMethods.add($m.tree);
        }
      | decl_field_set[listFields] /*a field set is something public like int x, y, z */
      )* /* * signifies that we are in a loop */
    ;

decl_field_set[ListDeclField listFields] /*a field st has one commun type and  visibility */
    : v=visibility t=type list_decl_field[$listFields, $t.tree, $v.vis]
      SEMI
    ;

visibility returns[Visibility vis]
    /*Check Visibility.java in tree/ there you'll find an enumerate type for Public + Protected */
    : /* epsilon */ {
            $vis = Visibility.PUBLIC;
        }
    | PROTECTED {
            $vis = Visibility.PROTECTED;
        }
    ;

list_decl_field[ListDeclField listFields, AbstractIdentifier t, Visibility vis]
    : dv1=decl_field[$t, $vis]
    /*field COMMA field is the only possible way of describing this ryule as if we simply loop onto fields we won't recognize the COMMA */
    {listFields.add($dv1.tree);}
        (COMMA dv2=decl_field[t, vis]
        {$listFields.add($dv2.tree);}
      )*
    ;

decl_field[AbstractIdentifier t, Visibility vis] returns[AbstractDeclField tree]
    /*I could hav* done the initialization in an @init section but i had the action section */
    /* We can either initialize the field or not */
    /*Initialization int x = 5 */
    : i=ident
      (EQUALS e=expr {
            Initialization init = new Initialization($e.tree);
            setLocation(init, $e.start);
            $tree = new DeclField($t, $i.tree, init,$vis);
        }
      )?
      {
        if ($tree == null) {
            NoInitialization init = new NoInitialization();
            setLocation(init, $i.start);
            $tree = new DeclField($t, $i.tree, init,$vis);
        }
        setLocation($tree, $i.start);
      }
    ;
    

decl_method returns[AbstractDeclMethod tree]
 @init {
     AbstractMethodBody body = null;
 }
     : t=type i=ident OPARENT params=list_params CPARENT /*So this reads for example void add(params)*/ 
     /*it is either a block of deca instructions or a succession of assemply instructions */
     (b=block {
         body = new MethodBody($b.decls, $b.insts);
         setLocation(body, $b.start); //ajout nécessaire, modif 3 janv
         }
       | ASM OPARENT code=multi_line_string CPARENT SEMI { 
         StringLiteral str = new StringLiteral($code.text);
         setLocation(str,$OPARENT);
         body = new MethodAsmBody(str);
         setLocation(body, $ASM);
         }
       ) {
             $tree = new DeclMethod($t.tree,$i.tree,$params.tree,body);
             setLocation($tree, $i.start); //ajout nécessaire, modif 3 janv
         }
   ;
/* //MODIF LE 1 JANV, À REVOIR
    : t=type i=ident OPARENT params=list_params CPARENT
      b=block
      {
        MethodBody body = new MethodBody($b.decls, $b.insts);
        setLocation(body, $b.start);

        $tree = new DeclMethod(
            $t.tree,
            $i.tree,
            $params.tree,
            body
        );
        setLocation($tree, $i.start);
      } */
    

list_params returns[ListDeclParam tree]
@init {
    $tree = new ListDeclParam();
}
/*Same logic for DeclField (the comma syntax) except here tree is local and doesn't come from another rule */
    : (p1=param {
        $tree.add($p1.tree);
        //setLocation($tree,$p1.start);
        } (COMMA p2=param {
        $tree.add($p2.tree);
        //setLocation($tree,$p2.start);
        }
      )*)?
    ;
    
multi_line_string returns[String text, Location location]
    : s=STRING {
            $text = $s.text;
            $location = tokenLocation($s);
        }
    | s=MULTI_LINE_STRING {
            $text = $s.text;
            $location = tokenLocation($s);
        }
    ;

param returns [AbstractDeclParam tree]
/*No need for initialization as we already know type and ident and DeclParam will take them directly */
    : t=type i=ident {
        $tree = new DeclParam($t.tree, $i.tree);
        setLocation($tree, $i.start);
        }
    ;
